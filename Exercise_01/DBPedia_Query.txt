
############DBpedia###############

##a##
--Question--
How tall is Claudia Schiffer?
• Classes: –
• Properties: dbo:height
• Individuals: res:Claudia_Schiffer

--Query--
PREFIX res:<http://dbpedia.org/resource/>
PREFIX dbo:<http://dbpedia.org/ontology/>
SELECT DISTINCT ?height
WHERE {
res:Claudia_Schiffer dbo:height ?height .
}

--Result--
------------
| callret-0|
============
|    6     |
------------



##b##
--Question--
Give me all female Russian astronauts.
• Classes: yago:RussianCosmonauts, yago:FemaleAstronauts
• Properties: –
• Individuals: –

--Query--
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
PREFIX yago:<http://dbpedia.org/class/yago/>
SELECT DISTINCT ?uri ?string
WHERE {
?uri rdf:type yago:RussianCosmonauts;
     rdf:type yago:FemaleAstronauts;
     rdfs:label ?string .
}


--Result--
--------------------------------------------------------------------------------
|                     uri                          |       string              |
================================================================================ http://dbpedia.org/resource/Valentina_Tereshkova   | "Valentina Tereshkova"@en | 
| http://dbpedia.org/resource/Yelena_Serova        | "Yelena Serova"@en        |
| http://dbpedia.org/resource/Yelena_Kondakova     | "Yelena Kondakova"@en     |
--------------------------------------------------------------------------------



##c##
--Question--
How many monarchical countries are there in Europe?
• Classes: yago:EuropeanCountries
• Properties: dbo:governmentType
• Individuals: –

--Query--
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
PREFIX yago:<http://dbpedia.org/class/yago/>
SELECT COUNT(DISTINCT ?uri)
WHERE {
?uri rdf:type yago:EuropeanCountries .
?uri dbo:governmentType ?govern.
FILTER regex(?govern ,'monarchy') .
}

--Result--
----------
| height |
==========
| 1.8034 |
----------



##d##
--Question--
Which states of Germany are governed by the Social Democratic Party?
• Classes: yago:StatesOfGermany
• Properties: dbp:rulingParty
• Individuals: res:Social_Democratic_Party_of_Germany

--Query--
PREFIX res:<http://dbpedia.org/resource/>
PREFIX dbp:<http://dbpedia.org/property/>
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
PREFIX yago:<http://dbpedia.org/class/yago/>
SELECT DISTINCT ?uri ?string
WHERE {
?uri rdf:type yago:StatesOfGermany .
{?uri dbp:rulingParty 'SPD'@en. }
UNION
{ ?uri dbp:rulingParty res:Social_Democratic_Party_of_Germany.}
?uri rdfs:label ?string.
}

--Result--
------------------------------------------------------------------------------------
|                     uri                            |        string               |
==================================================================================== | http://dbpedia.org/resource/Hamburg                | "Hamburg"@en                | 
| http://dbpedia.org/resource/Lower_Saxony	     | "Lower Saxony"@en           |
| http://dbpedia.org/resource/Thuringia	             | "Thuringia"@en              |
| http://dbpedia.org/resource/Brandenburg	     | "Brandenburg"@en            |
| http://dbpedia.org/resource/Saxony-Anhalt	     | "Saxony-Anhalt"@en          |
| http://dbpedia.org/resource/Rhineland-Palatinate   | "Rhineland-Palatinate"@en   |
| http://dbpedia.org/resource/Saarland	             | "Saarland"@en               |
| http://dbpedia.org/resource/Mecklenburg-Vorpommern | "Mecklenburg-Vorpommern"@en | 
| http://dbpedia.org/resource/Bremen_(state)	     | "Bremen (state)"@en         |
| http://dbpedia.org/resource/Berlin	             | "Berlin"@en                 |     
| http://dbpedia.org/resource/Saxony	             | "Saxony"@en                 |
------------------------------------------------------------------------------------



##e##
--Question--
Which monarchs of the United Kingdom were married to a German?
• Classes: yago:MonarchsOfTheUnitedKingdom
• Properties: dbo:spouse, dbo:birthPlace
• Individuals: res:Germany

--Query--
PREFIX res:<http://dbpedia.org/resource/>
PREFIX dbo:<http://dbpedia.org/ontology/>
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
PREFIX yago:<http://dbpedia.org/class/yago/>
SELECT DISTINCT ?uri 
WHERE {
?uri rdf:type yago:MonarchsOfTheUnitedKingdom;
     dbo:spouse ?spouse;
     rdfs:label ?string.
?spouse dbo:birthPlace res:Germany.
}

--Result--
No result



##f##
--Question--
Which countries have places with more than two caves?
• Classes: dbo:Cave, dbo:Country
• Properties: dbo:location
• Individuals:-

--Query--
PREFIX dbo:<http://dbpedia.org/ontology/>
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?uri ?string
WHERE {
?cave rdf:type dbo:Cave;
      dbo:location ?uri.
?uri rdf:type dbo:Country;
     rdfs:label ?string.
}
GROUP BY ?uri ?string
HAVING(COUNT(?cave ) > 2)

--Result--
----------------------------------------------------------------
|                     uri                  |     string        |
================================================================ 
| http://dbpedia.org/resource/Austria	   | "Austria"@en      |
| http://dbpedia.org/resource/France	   | "France"@en       |
| http://dbpedia.org/resource/Greece	   | "Greece"@en       |
| http://dbpedia.org/resource/Philippines  | "Philippines"@en  |
| http://dbpedia.org/resource/South_Africa | "South Africa"@en |
| http://dbpedia.org/resource/Turkey	   | "Turkey"@en       |
| http://dbpedia.org/resource/United_States| "United States"@en|
| http://dbpedia.org/resource/Serbia	   | "Serbia"@en       |
| http://dbpedia.org/resource/Vietnam	   | "Vietnam"@en      | 
| http://dbpedia.org/resource/Brazil	   | "Brazil"@en       |
| http://dbpedia.org/resource/Gibraltar	   | "Gibraltar"@en    |
| http://dbpedia.org/resource/Israel	   | "Israel"@en       |
| http://dbpedia.org/resource/Mexico	   | "Mexico"@en       |
| http://dbpedia.org/resource/Spain	   | "Spain"@en        |
| http://dbpedia.org/resource/China	   | "China"@en        |
| http://dbpedia.org/resource/Jamaica	   | "Jamaica"@en      |
| http://dbpedia.org/resource/Australia	   | "Australia"@en    |
| http://dbpedia.org/resource/Germany	   | "Germany"@en      |
| http://dbpedia.org/resource/Italy	   | "Italy"@en        |
----------------------------------------------------------------




##g##
--Question--
Give me all cities in New Jersey with more than 100000 inhabitants.
• Classes: dbo:City
• Properties: dbo:isPartOf, dbp:populationTotal
• Individuals: res:New_Jersey

--Query--
PREFIX dbo:<http://dbpedia.org/ontology/>
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX res:<http://dbpedia.org/resource/>
PREFIX dbp:<http://dbpedia.org/property/>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?uri ?string
WHERE {
?uri rdf:type dbo:City;
     dbo:isPartOf res:New_Jersey;
     dbp:populationTotal ?inhabitants;
     rdfs:label ?string.
FILTER(?inhabitants > 100000) 
}

--Result--
-----------------------------------------------------------------------------------
|                     uri                            |         string             |
===================================================================================
| http://dbpedia.org/resource/Newark,_New_Jersey     |"Newark, NewJersey"@en      |
| http://dbpedia.org/resource/Paterson,_New_Jersey   |"Paterson, New Jersey"@en   |
| http://dbpedia.org/resource/Elizabeth,_New_Jersey  |"Elizabeth, New Jersey"@en  |
| http://dbpedia.org/resource/Jersey_City,_New_Jersey|"Jersey City, New Jersey"@en|
-----------------------------------------------------------------------------------



##h##
--Question--
Is proinsulin a protein?
• Classes: dbo:Protein
• Properties: –
• Individuals: res:Proinsulin

--Query--
PREFIX dbo:<http://dbpedia.org/ontology/>
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX res:<http://dbpedia.org/resource/>
ASK
WHERE {
res:Proinsulin rdf:type dbo:Protein.
}

--Result--
true



##i##
--Question--
Is Frank Herbert still alive?
• Classes: –
• Properties: dbo:deathDate
• Individuals: res:Frank_Herbert

--Query--
PREFIX dbo:<http://dbpedia.org/ontology/>
PREFIX res:<http://dbpedia.org/resource/>
ASK WHERE{
OPTIONAL{res:Frank_Herbert dbo:deathDate ?date.}
FILTER(!BOUND(?date))
}

--Result--
false



##j##
--Question--
Which mountain is the highest after the Annapurna?
• Classes: dbo:Mountain
• Properties: dbo:elevation
• Individuals: res:Annapurna

--Query--
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
PREFIX dbo:<http://dbpedia.org/ontology/>
PREFIX res:<http://dbpedia.org/resource/>
SELECT DISTINCT ?uri ?string
WHERE {
res:Annapurna dbo:elevation ?elevation.
?uri rdf:type dbo:Mountain;
     dbo:elevation ?otherelevation;
     rdfs:label ?string.
FILTER(?otherelevation < ?elevation)
}
ORDER BY DESC(?otherelevation) LIMIT 1

--Result--
No result

